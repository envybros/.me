---
title: "SUI 개발 베이직"
categories: [Web3 연구소]
tags: [Sui, Move]
date: 2023-07-25 00:10
img_path: /assets/img/blockchains/
---

---

![Sui Img](sui_back.png)

## **Sui 설치하기**

해당 링크를 타고 가서 Sui와 Move를 설치해보자. [설치 링크](https://docs.sui.io/build/install#install-sui-binaries)

```bash
cargo install --locked --git https://github.com/MystenLabs/sui.git --branch devnet sui
```

---

## **들어가기 전에**

Sui Move의 기본적인 주요 기능에 대해 설명하는 곳이다. 오른쪽 네비게이션을 통해, 보고 싶은 내용을 찾아갈 수 있도록 하자.

---

## **Move.toml**

모든 Move 패키지에는 `Move.toml` 파일 형식의 패키지 매니패스트가 있다. 또한 이 파일은 페이지의 루트에 위치한다. `Move.toml`에는 여러 섹션이 포함되어 있으며, 주요 섹션은 다음과 같다.

- [package] - 이름, 작성자 등의 패키지 메타데이터를 포함한다.
- [dependencies] - 프로젝트의 종속성을 지정한다.
- [addresses] - 주소 별칭(aliases) (예: @me는 0x0 주소로 취급된다.)

```toml
[package]
name = "sui-by-example"
version = "0.1.0"

[dependencies]
Sui = { git = "https://github.com/MystenLabs/sui.git", subdir = "crates/sui-framework/packages/sui-framework", rev = "main" }

[addresses]
examples = "0x0"
```

---

## **초기화 함수 (Init)**

초기화 함수는 관련 모듈이 발행될 때 한 번만 실행되는 특수 함수이다. 항상 동일한 서명과 하나의 인자만을 갖는다.

```rust
fun init(ctx: &mut TxContext) { /* ... */ }
```

```rust
module examples::one_timer {
    use sui::transfer;
    use sui::object::{Self, UID};
    use sui::tx_context::{Self, TxContext};

    /// 모듈 이니셜라이저에서 생성되는 유일한 것
    struct CreatorCapability has key {
        id: UID
    }

    /// 이 함수는 모듈을 게시할 때 한 번만 호출된다.
    /// 다음과 같이 어떤 일이 한 번만 발생했는지 확인하려면 이 함수를 사용해보자.
    /// - 오직 모듈 작성자만 `CreatorCapability` 구조체의 버전을 소유할 수 있다.
    fun init(ctx: &mut TxContext) {
        transfer::transfer(CreatorCapability {
            id: object::new(ctx),
        }, tx_context::sender(ctx))
    }
}
```

---

## **문자열 (Strings)**

Move에는 문자열에 대한 기본 타입이 없다. 하지만 편리한 래퍼가 있다!!

```rust
module examples::strings {
    use sui::object::{Self, UID};
    use sui::tx_context::TxContext;

    // 이 종속성을 사용하여 UTF-8 문자열에 대한 타입 래퍼를 가져올 수 있다.
    use std::string::{Self, String};

    /// 문자열 타입을 보유하는 더미 객체
    struct Name has key, store {
        id: UID,

        /// String 타입이다!
        name: String
    }

    /// 원시 바이트를 전달하여 이름 객체를 생성할 수 있다.
    public fun issue_name_nft(
        name_bytes: vector<u8>, ctx: &mut TxContext
    ): Name {
        Name {
            id: object::new(ctx),
            name: string::utf8(name_bytes)
        }
    }
}
```

---

## **공유 객체**

공유 객체는 `sui::transfer::share_object` 함수를 사용하여 공유되는 객체이다. 이는 모든 사람이 접근할 수 있다.

```rust
/// "소유" 객체와 달리 "공유" 객체는 네트워크의 모든 사람이 액세스할 수 있다.
/// 이러한 종류의 오브젝트의 확장된 기능과 접근성을 사용하기 위해서는,
/// 필요한 경우 액세스를 보호하는 등의 추가적인 작업이 필요하다.
module examples::donuts {
    use sui::transfer;
    use sui::sui::SUI;
    use sui::coin::{Self, Coin};
    use sui::object::{Self, UID};
    use sui::balance::{Self, Balance};
    use sui::tx_context::{Self, TxContext};

    /// 코인 잔액이 너무 낮을 때 사용
    const ENotEnough: u64 = 0;

    /// 소유자에게 수익금 수령 권한을 부여하는 기능
    struct ShopOwnerCap has key { id: UID }

    /// 구매 가능한 도넛이다. 단순화를 위해 구현은 생략
    struct Donut has key { id: UID }

    /// 공유 객체. `key` 어빌리티가 필요하다.
    struct DonutShop has key {
        id: UID,
        price: u64,
        balance: Balance<SUI>
    }

    /// Init 함수는 한 번만 호출되므로 공유 객체를 초기화하는 데 이상적인 장소이다.
    ///
    /// 객체를 공유하기 위해 `transfer::share_object`가 사용된다.
    fun init(ctx: &mut TxContext) {
        transfer::transfer(ShopOwnerCap {
            id: object::new(ctx)
        }, tx_context::sender(ctx));

        // 객체를 공유하여 모든 사람이 액세스할 수 있도록 해보자!
        transfer::share_object(DonutShop {
            id: object::new(ctx),
            price: 1000,
            balance: balance::zero()
        })
    }

    /// 코인을 소유한 모든 사람이 사용할 수 있는 입력 기능
    public entry fun buy_donut(
        shop: &mut DonutShop, payment: &mut Coin<SUI>, ctx: &mut TxContext
    ) {
        assert!(coin::value(payment) >= shop.price, ENotEnough);

        // Coin<SUI>으로부터 amount = `shop.price`을 받아오자.
        let coin_balance = coin::balance_mut(payment);
        let paid = balance::split(coin_balance, shop.price);

        // 상점의 잔액(balance)에 코인 넣기
        balance::join(&mut shop.balance, paid);

        transfer::transfer(Donut {
            id: object::new(ctx)
        }, tx_context::sender(ctx))
    }

    /// 도넛을 먹고, 아무것도 얻지 못한다....
    public entry fun eat_donut(d: Donut) {
        let Donut { id } = d;
        object::delete(id);
    }

    /// `DonutShop`에서 코인을 가져와 tx발신자에게 전송한다.
    /// `ShopOwnerCap` 인증이 필요하다.
    public entry fun collect_profits(
        _: &ShopOwnerCap, shop: &mut DonutShop, ctx: &mut TxContext
    ) {
        let amount = balance::value(&shop.balance);
        let profits = coin::take(&mut shop.balance, amount, ctx);

        transfer::public_transfer(profits, tx_context::sender(ctx))
    }
}
```

---

## **전송(Transfer)**

객체를 자유롭게 전송할 수 있게 하려면 키와 저장소 능력을 조합하여 사용해야 한다.

```rust
/// 사용자 지정 데이터를 자유롭게 전송할 수 있는 래퍼
module examples::wrapper {
    use sui::object::{Self, UID};
    use sui::tx_context::TxContext;

    /// `store`가 있는 객체는 사용자 지정 전송 구현 없이 
    /// 모든 모듈에서 전송할 수 있다.
    struct Wrapper<T: store> has key, store {
        id: UID,
        contents: T
    }

    /// 'Container'의 내용을 읽는 `read` 기능이다.
    public fun contents<T: store>(c: &Wrapper<T>): &T {
        &c.contents
    }

    /// 누구나 새 객체를 만들 수 있다.
    public fun create<T: store>(
        contents: T, ctx: &mut TxContext
    ): Wrapper<T> {
        Wrapper {
            contents,
            id: object::new(ctx),
        }
    }

    /// `래퍼`를 제거하고, `T`를 얻는다.
    public fun destroy<T: store> (c: Wrapper<T>): T {
        let Wrapper { id, contents } = c;
        object::delete(id);
        contents
    }
}

module examples::profile {
    use sui::transfer;
    use sui::url::{Self, Url};
    use std::string::{Self, String};
    use sui::tx_context::{Self, TxContext};

    // 래퍼 함수를 사용해보자.
    use 0x0::wrapper;

    /// 객체가 아닌 프로필 정보를 전송 가능한 컨테이너에 래핑할 수 있다.
    struct ProfileInfo has store {
        name: String,
        url: Url
    }

    /// `ProfileInfo`에서 `name` 필드를 읽는다.
    public fun name(info: &ProfileInfo): &String {
        &info.name
    }

    /// `ProfileInfo`에서 `url`필드를 읽는다.
    public fun url(info: &ProfileInfo): &Url {
        &info.url
    }

    /// 새 `ProfileInfo`를 생성하고 `Wrapper`로 래핑한다.
    /// 그런 다음 발신자에게 전송한다.
    public fun create_profile(
        name: vector<u8>, url: vector<u8>, ctx: &mut TxContext
    ) {
        // 새 컨테이너를 생성하고 ProfileInfo를 래핑한다.
        let container = wrapper::create(ProfileInfo {
            name: string::utf8(name),
            url: url::new_unsafe_from_bytes(url)
        }, ctx);

        // `Wrapper` 타입은 자유롭게 전송할 수 있다.
        transfer::public_transfer(container, tx_context::sender(ctx))
    }
}
```

---

## **사용자 지정 전송**

Sui Move에서는 기본적으로 `key` 기능으로만 정의된 객체를 전송할 수 없다. 그러므로 전송을 활성화하려면 퍼블리셔가 사용자 지정 전송 함수를 만들어야 한다. 이 함수에는 사용자가 전송을 위해 지불해야 하는 모든 인수(예: 수수료)를 포함할 수 있다.

```rust
module examples::restricted_transfer {
    use sui::tx_context::{Self, TxContext};
    use sui::balance::{Self, Balance};
    use sui::coin::{Self, Coin};
    use sui::object::{Self, UID};
    use sui::transfer;
    use sui::sui::SUI;

    /// 결제 금액이 전송 가격과 같지 않을 때 사용한다.
    const EWrongAmount: u64 = 0;

    /// 무기명 사용자가 새로운 `TitleDeed(타이틀 증서)`들을 생성할 수 있는 기능.
    struct GovernmentCapability has key { id: UID }

    /// 속성 소유권을 표시하는 객체
    /// 권한이 있는 기관에서만 발급할 수 있다.
    struct TitleDeed has key {
        id: UID,
        // ... 몇 가지 추가 필드
    }

    /// 속성 소유권 이전을 승인하고 수수료를 징수하는
    /// 중앙 집중식 레지스트리.
    struct LandRegistry has key {
        id: UID,
        balance: Balance<SUI>,
        fee: u64
    }

    /// 모듈 초기화 시 `LandRegistry`를 생성한다.
    fun init(ctx: &mut TxContext) {
        transfer::transfer(GovernmentCapability {
            id: object::new(ctx)
        }, tx_context::sender(ctx));

        transfer::share_object(LandRegistry {
            id: object::new(ctx),
            balance: balance::zero<SUI>(),
            fee: 10000
        })
    }

    /// `TitleDeed`를 생성하고 부동산 소유자에게 전송한다.
    /// 이 작업은 `GovernmentCapability`의 소유자만 수행할 수 있다.
    public entry fun issue_title_deed(
        _: &GovernmentCapability,
        for: address,
        ctx: &mut TxContext
    ) {
        transfer::transfer(TitleDeed {
            id: object::new(ctx)
        }, for)
    }

    /// 사용자 정의 전송 함수. `TitleDeed`에는 
    /// `store` 기능이 없기 때문에 이것이 필요하다. 
    /// 모든 전송은 이 함수를 거쳐 `LandRegistry`에 수수료를 지불해야 한다.
    public entry fun transfer_ownership(
        registry: &mut LandRegistry,
        paper: TitleDeed,
        fee: Coin<SUI>,
        to: address,
    ) {
        assert!(coin::value(&fee) == registry.fee, EWrongAmount);

        // LandRegistry 잔액에 결제 금액 추가하기
        balance::join(&mut registry.balance, coin::into_balance(fee));

        // 마지막으로 전송 함수를 호출.
        transfer::transfer(paper, to)
    }
}
```

---

## **이벤트**

이벤트는 체인에서 작업을 추적할 수 있는 주요 방법이다.

```rust
/// 공유 객체의 확장된 예시. 이제 이벤트가 추가되었다!
module examples::donuts_with_events {
    use sui::transfer;
    use sui::sui::SUI;
    use sui::coin::{Self, Coin};
    use sui::object::{Self, ID, UID};
    use sui::balance::{Self, Balance};
    use sui::tx_context::{Self, TxContext};

    // 이벤트에 필요한 유일한 종속성
    use sui::event;

    /// 코인 잔액이 너무 부족한 경우를 위해
    const ENotEnough: u64 = 0;

    /// 소유자에게 수익을 징수할 수 있는 권한을 부여하는 기능.
    struct ShopOwnerCap has key { id: UID }

    /// 구매 가능한 도넛. 단순화를 위해 구현은 생략한다.
    struct Donut has key { id: UID }

    struct DonutShop has key {
        id: UID,
        price: u64,
        balance: Balance<SUI>
    }

    // ====== Events ======

    /// 누군가 도넛을 구매했을 때를 위해
    struct DonutBought has copy, drop {
        id: ID
    }

    /// 도넛샵 소유자가 수익을 모았을 때를 위해
    struct ProfitsCollected has copy, drop {
        amount: u64
    }

    // ====== Functions ======

    fun init(ctx: &mut TxContext) {
        transfer::transfer(ShopOwnerCap {
            id: object::new(ctx)
        }, tx_context::sender(ctx));

        transfer::share_object(DonutShop {
            id: object::new(ctx),
            price: 1000,
            balance: balance::zero()
        })
    }

    /// 도넛 구매
    public entry fun buy_donut(
        shop: &mut DonutShop, payment: &mut Coin<SUI>, ctx: &mut TxContext
    ) {
        assert!(coin::value(payment) >= shop.price, ENotEnough);

        let coin_balance = coin::balance_mut(payment);
        let paid = balance::split(coin_balance, shop.price);
        let id = object::new(ctx);

        balance::join(&mut shop.balance, paid);

        // 미래 객체의 ID를 사용하여 이벤트를 발생시킨다.
        event::emit(DonutBought { id: object::uid_to_inner(&id) });
        transfer::transfer(Donut { id }, tx_context::sender(ctx))
    }

    /// 도넛을 소비하고 아무것도 얻지 못한다...
    public entry fun eat_donut(d: Donut) {
        let Donut { id } = d;
        object::delete(id);
    }

    /// `도넛샵`에서 코인을 가져와 tx 발신자에게 전송한다.
    /// `ShopOwnerCap`으로 인증이 필요하다.
    public entry fun collect_profits(
        _: &ShopOwnerCap, shop: &mut DonutShop, ctx: &mut TxContext
    ) {
        let amount = balance::value(&shop.balance);
        let profits = coin::take(&mut shop.balance, amount, ctx);

        // 새 타입 인스턴스를 생성하고 발생시키기만 하면 된다.
        event::emit(ProfitsCollected { amount });

        transfer::public_transfer(profits, tx_context::sender(ctx))
    }
}
```

---

## **일회성 증인(One Time Witness)**

일회성 증인(OTW)는 모듈 이니셜라이저에서만 생성되는 타입의 특수 인스턴스로, 단 하나의 고유한 인스턴스만 존재하도록 보장된다. 증인 승인 작업이 한 번만 수행되었는지 확인해야 하는 경우 (예: [새 코인 생성](https://examples.sui.io/samples/coin.html))에 중요한 역할을 한다. Sui Move에서 타입은 정의에 다음과 같은 속성이 있는 경우, OTW로 간주된다:

- 모듈의 이름을 따서 명명하되 대문자로 표기
- 오직 `drop` 기능만 갖고 있다.

> 인스턴스가 OTW인지 확인하려면 `sui::types::is_one_time_witness(witness)`를 사용해야 한다.
{: .prompt-tip }

이 타입의 인스턴스를 가져오려면 `init()` 함수에 첫 번째 인수로 추가해야 한다: Sui 런타임은 두 이니셜라이저 인수를 자동으로 제공한다.

```rust
module examples::mycoin {

    /// 모듈 이름과 일치하는 이름
    struct MYCOIN has drop {}

    /// 인스턴스가 첫 번째 인수로 수신된다.
    fun init(witness: MYCOIN, ctx: &mut TxContext) {
        /* ... */
    }
}
```

OTW를 어떻게 사용할 수 있는지 보여주는 예시:

```rust
/// 이 예시는 일회성 증인이 어떻게 작동하는지 보여준다.
///
/// 원타임 증인(OTW)은 시스템 전체에서 고유하도록
/// 보장되는 타입의 인스턴스이다. 다음과 같은 속성을 가진다:
///
/// - 모듈 이니셜라이저에서만 생성됨
/// - 모듈의 이름을 따서 명명됨(대문자)
/// - 수동으로 패킹할 수 없다.
/// - `drop` 기능이 있다.
module examples::one_time_witness_registry {
    use sui::tx_context::TxContext;
    use sui::object::{Self, UID};
    use std::string::String;
    use sui::transfer;

    // 이 종속성을 통해 타입이 
    // 일회성 증인(OTW)인지 여부를 확인할 수 있다.
    use sui::types;

    /// 누군가 OTW가 아닌 구조체를 보내려고 할 때를 대비
    const ENotOneTimeWitness: u64 = 0;

    /// 이 타입의 객체는 타입이 있음을 표시하며, 
    /// 타입당 하나의 레코드만 가질 수 있다.
    struct UniqueTypeRecord<phantom T> has key {
        id: UID,
        name: String
    }

    /// 사용자 정의 이름으로 새 타입을 등록할 수 있도록 공개 함수를 노출한다. 
    /// `is_one_time_witness` 호출을 사용하면 단일 `T`에 대해 
    /// 이 함수를 한 번만 호출할 수 있도록 한다.
    public fun add_record<T: drop>(
        witness: T,
        name: String,
        ctx: &mut TxContext
    ) {
        // 이 호출을 통해 타입이 OTW인지 확인할 수 있다;
        assert!(types::is_one_time_witness(&witness), ENotOneTimeWitness);

        // 전 세계가 볼 수 있도록 기록을 공유해보자 :)
        transfer::share_object(UniqueTypeRecord<T> {
            id: object::new(ctx),
            name
        });
    }
}

/// OTW 생성 예시
module examples::my_otw {
    use std::string;
    use sui::tx_context::TxContext;
    use examples::one_time_witness_registry as registry;

    /// 타입은 모듈의 이름을 따서 명명하되 대문자로 표기.
    struct MY_OTW has drop {}

    /// 이를 얻으려면 모듈 이니셜라이저의 첫 번째 인수를 사용합다.
    /// 참조 타입이 아닌 전체 인스턴스이다.
    fun init(witness: MY_OTW, ctx: &mut TxContext) {
        registry::add_record(
            witness, // 이 부분 확인
            string::utf8(b"My awesome record"),
            ctx
        )
    }
}
```

---

## **퍼블리셔**

퍼블리셔 객체는 퍼블리셔 권한을 나타내는 방법으로 사용된다. 이 객체 자체는 특정 사용 사례를 의미하지 않으며, 두 가지 주요 함수, 즉 `package::from_module<T>`와 `package::from_package<T>`가 있어 타입 `T`가 퍼블리셔 객체가 생성된 모듈 또는 패키지에 속하는지 확인할 수 있다.

새로운 객체를 정의하는 대부분의 패키지는 "Display"를 설정하고 "Kiosk" 생태계에서 해당 타입이 거래될 수 있도록 허용하는 데 필요한 `publisher` 객체를 발행할 것을 강력히 권장한다.

> `publisher` 자체는 유틸리티이지만, `소유권 증명` 기능(예: 객체 디스플레이에 필수적인 기능)을 사용할 수 있다.
{: .prompt-tip }

퍼블리셔를 설정하려면 일회성 증인(OTW)이 필요하다다. 이렇게 하면 특정 모듈에 대해 `Publisher` 개체가 한 번만 초기화되고(패키지의 경우 여러 번일 수 있음), 생성 함수가 게시 트랜잭션에서 호출되도록 할 수 있다.

```rust
/// OTW를 정의하고 발신자에 대한 `Publisher` 객체를 요청하는 간단한 패키지
module examples::owner {
    use sui::tx_context::TxContext;
    use sui::package;

    /// OTW는 `drop`만 있는 구조체이며
    /// 모듈의 이름을 따서 대문자로 명명된다.
    /// 자세한 내용은 "일회성 증인" 내용을 참조해보자.
    struct OWNER has drop {}

    /// 더미 확인에 사용할 또 다른 타입
    struct ThisType {}

    /// 모듈이 게시된 후 발신자는 `Publisher` 객체를 받게 된다. 
    /// 이 객체를 사용하여 `Kiosk` 시스템에서 전송 정책을 표시하거나
    /// 관리하는 데 사용할 수 있다.
    fun init(otw: OWNER, ctx: &mut TxContext) {
        package::claim_and_keep(otw, ctx)
    }
}

/// `Publisher` 객체를 사용하여 감사 토큰과 
/// 소유한 타입에 대한 `TypeOwnerCap`을 제공하는 모듈
module examples::type_owner {
    use sui::object::{Self, UID};
    use sui::tx_context::TxContext;
    use sui::package::{Self, Publisher};

    /// 잘못된 `Publisher`가 있는 타입에 대한 소유권을 주장하려고 한다.
    const ENotOwner: u64 = 0;

    /// 자신의 소유권을 "객관적으로" 확인하고자 하는 
    /// 사용자에게 부여되는 기능 :)
    struct TypeOwnerCap<phantom T> has key, store {
        id: UID
    }

    /// `Publisher` 객체를 사용하여 호출자가 `T` 타입을 소유하고 있는지 확인.
    public fun prove_ownership<T>(
        publisher: &Publisher, ctx: &mut TxContext
    ): TypeOwnerCap<T> {
        assert!(package::from_package<T>(publisher), ENotOwner);
        TypeOwnerCap<T> { id: object::new(ctx) }
    }
}
```

---

## **객체 디스플레이**

오브젝트 표시

`publicher` 객체를 소유한 크리에이터 또는 빌더는 `sui::display` 모듈을 사용하여 객체에 대한 표시 속성을 정의할 수 있다. 

`Display<T>`는 `T` 타입에 대한 명명된 템플릿 집합을 지정하는 객체이다(예를 들어 `0x2::capy::Capy` 타입의 경우 디스플레이는 `Display<0x2::capy::Capy>`가 된다). `T` 타입의 모든 객체는 일치하는 디스플레이 정의를 통해 Sui 풀 노드 RPC에서 처리되며, 객체가 쿼리될 때 처리된 결과가 첨부된다.

### **설명**

Sui 객체 디스플레이는 템플릿 엔진으로, 오프체인에서 처리할 수 있는 타입을 온체인에서 디스플레이로 구성할 수 있게 해준다. 템플릿 문자열로 대체하기 위해 객체의 데이터를 사용할 수 있는 기능이 있다.

설정 간능한 필드에는 제한이 없으며, 모든 객체 속성은 `{property}` 구문을 통해 액세스하고 템플릿 문자열의 일부로 삽입할 수 있다.

### **예시**

다음 히어로 모듈의 경우 "Hero" 타입의 "name", "id", "image_url" 속성에 따라 표시가 달라진다. 초기화 함수에서 정의된 템플릿은 다음과 같이 표현할 수 있다:

```json
{
    "name": "{name}",
    "link": "https://sui-heroes.io/hero/{id}",
    "image_url": "ipfs://{img_url}",
    "description": "A true Hero of the Sui ecosystem!",
    "project_url": "https://sui-heroes.io",
    "creator": "Unknown Sui Fan"
}
```

```rust
/// 누구나 자유롭게 히어로를 발행할 수 있는 무제한 "Sui Hero" 컬렉션의 예시이다.
/// `Publisher`를 초기화하는 방법과 이를 사용하여 생태계의 타입을 설명하는 방법인 
/// `Display<Hero>` 객체를 얻는 방법을 보여준다.
module examples::my_hero {
    use sui::tx_context::{sender, TxContext};
    use std::string::{utf8, String};
    use sui::transfer;
    use sui::object::{Self, UID};

    // The creator bundle: 이 두 패키지는 종종 함께 사용된다.
    use sui::package;
    use sui::display;

    /// The Hero - 뛰어난 디지털 아트 컬렉션.
    struct Hero has key, store {
        id: UID,
        name: String,
        img_url: String,
    }

    /// 모듈에 대한 일회성 증인(OTW).
    struct MY_HERO has drop {}

    /// 모듈 이니셜라이저에서 `Publisher` 객체를 요청한 다음
    /// `Display`를 생성한다. `Display`는 필드 집합으로 초기화되고
    /// (나중에 수정할 수 있음) `update_version` 호출을 통해 게시된다.
    ///
    /// 키와 값은 이니셜라이저에서 설정되지만 
    /// `Publisher` 객체가 생성된 경우 게시 후에도 설정할 수 있다.
    fun init(otw: MY_HERO, ctx: &mut TxContext) {
        let keys = vector[
            utf8(b"name"),
            utf8(b"link"),
            utf8(b"image_url"),
            utf8(b"description"),
            utf8(b"project_url"),
            utf8(b"creator"),
        ];

        let values = vector[
            // `name`의 경우 `Hero.name` 속성을 사용할 수 있다.
            utf8(b"{name}"),
            // `link`의 경우 `id` 속성을 사용하여 URL을 작성할 수 있다.
            utf8(b"https://sui-heroes.io/hero/{id}"),
            // `image_url`의 경우 IPFS 템플릿 + `img_url` 속성을 사용한다.
            utf8(b"ipfs://{img_url}"),
            // 설명은 모든 `Hero` 객체에 대해 정적이다.
            utf8(b"A true Hero of the Sui ecosystem!"),
            // 프로젝트 URL은 일반적으로 정적이다.
            utf8(b"https://sui-heroes.io"),
            // 크리에이터 필드는 무엇이든 가능하다.
            utf8(b"Unknown Sui Fan")
        ];

        // 패키지에 대한 `Publisher`를 청구하자!
        let publisher = package::claim(otw, ctx);

        // `Hero` 타입에 대한 새로운 `Display` 객체를 가져온다.
        let display = display::new_with_fields<Hero>(
            &publisher, keys, values, ctx
        );

        // 변경 사항을 적용하기 위해 첫 번째 버전의 `Display`를 커밋한다.
        display::update_version(&mut display);

        transfer::public_transfer(publisher, sender(ctx));
        transfer::public_transfer(display, sender(ctx));
    }

    /// 누구나 'Hero'를 발행할 수 있다!
    public fun mint(name: String, img_url: String, ctx: &mut TxContext): Hero {
        let id = object::new(ctx);
        Hero { id, name, img_url }
    }
}
```

### **메서드 설명**

디스플레이는 사용자 정의 함수(또는 모듈 이니셜라이저)에서 또는 프로그래밍 가능한 트랜잭션의 일부로 수행될 수 있는 `display::new<T>` 호출을 통해 생성된다.

```rust
module sui::display {
    /// `T`에 대한 새 디스플레이 객체를 가져온다.
    /// 퍼블리셔는 T의 퍼블리셔여야 하며, `from_package` 확인이 수행된다.
    public fun new<T>(pub: &Publisher): Display<T> { /* ... */ }
}
```

획득한 후에는 디스플레이를 수정할 수 있다:

```rust
module sui::display {
    /// 한 번에 여러 필드를 설정한다.
    public fun add_multiple(
        self: &mut Display,
        keys: vector<String>,
        values: vector<String
    ) { /* ... */ }

    /// 단일 필드 편집
    public fun edit(self: &mut Display, key: String, value: String) { /* ... */ }

    /// Display에서 키 제거
    public fun remove(self: &mut Display, key: String ) { /* ... */ }
}
```

변경 사항을 적용하고 T에 대한 표시를 설정하려면 마지막으로 한 번의 호출이 필요하다. `update_version`은 풀 노드가 수신 대기하고 타입에 대한 템플릿을 가져오는 데 사용하는 이벤트를 배포하여 버전을 게시한다.

```rust
module sui::display {
    /// 디스플레이 버전을 업데이트하고 이벤트를 배포한다.
    public fun update_version(self: &mut Display) { /* ... */ }
}
```

---

참고 자료:

- [Sui Examples](https://examples.sui.io/index.html)
